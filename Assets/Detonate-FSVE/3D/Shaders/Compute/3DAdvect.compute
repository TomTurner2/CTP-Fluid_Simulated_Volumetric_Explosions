float4 size;
float dt;
float dissipation;
float decay;
float forward;

StructuredBuffer<float3> velocity;
StructuredBuffer<float> obstacles;

RWStructuredBuffer<float3> write_RGB;//render textures with RG channel to represent vector components
StructuredBuffer<float3> read_RGB;

RWStructuredBuffer<float> write_R;//render textures with only R channel to store single values
StructuredBuffer<float> read_R;


float SampleBilinear(StructuredBuffer<float> _grid, float3 _coord, float3 size)
{
	int x = _coord.x;
	int y = _coord.y;
	int z = _coord.z;
	
	int X = size.x;
	int XY = size.x*size.y;
	
	float fx = _coord.x-x;
	float fy = _coord.y-y;
	float fz = _coord.z-z;
	
	int xp1 = min(size.x-1, x+1);
	int yp1 = min(size.y-1, y+1);
	int zp1 = min(size.z-1, z+1);
	
	float x0 = _grid[x+y*X+z*XY] * (1.0f-fx) + _grid[xp1+y*X+z*XY] * fx;
	float x1 = _grid[x+y*X+zp1*XY] * (1.0f-fx) + _grid[xp1+y*X+zp1*XY] * fx;
	
	float x2 = _grid[x+yp1*X+z*XY] * (1.0f-fx) + _grid[xp1+yp1*X+z*XY] * fx;
	float x3 = _grid[x+yp1*X+zp1*XY] * (1.0f-fx) + _grid[xp1+yp1*X+zp1*XY] * fx;
	
	float z0 = x0 * (1.0f-fz) + x1 * fz;
	float z1 = x2 * (1.0f-fz) + x3 * fz;
	
	return z0 * (1.0f-fy) + z1 * fy;
}


float3 SampleBilinear(StructuredBuffer<float3> _grid, float3 _coord, float3 size)
{
	int x = _coord.x;
	int y = _coord.y;
	int z = _coord.z;
	
	int X = size.x;
	int XY = size.x*size.y;
	
	float fx = _coord.x-x;
	float fy = _coord.y-y;
	float fz = _coord.z-z;
	
	int xp1 = min(size.x-1, x+1);
	int yp1 = min(size.y-1, y+1);
	int zp1 = min(size.z-1, z+1);
	
	float3 x0 = _grid[x+y*X+z*XY] * (1.0f-fx) + _grid[xp1+y*X+z*XY] * fx;
	float3 x1 = _grid[x+y*X+zp1*XY] * (1.0f-fx) + _grid[xp1+y*X+zp1*XY] * fx;
	
	float3 x2 = _grid[x+yp1*X+z*XY] * (1.0f-fx) + _grid[xp1+yp1*X+z*XY] * fx;
	float3 x3 = _grid[x+yp1*X+zp1*XY] * (1.0f-fx) + _grid[xp1+yp1*X+zp1*XY] * fx;
	
	float3 z0 = x0 * (1.0f-fz) + x1 * fz;
	float3 z1 = x2 * (1.0f-fz) + x3 * fz;
	
	return z0 * (1.0f-fy) + z1 * fy;
}


float3 GetAdvectedPosition(float3 _position, int _index)
{
	_position -= dt * forward * velocity[_index];
	return _position;
}


int GetIndex(int3 _id, float4 _size)
{
	return _id.x + _id.y * _size.x + _id.z * _size.x * _size.y;
}


#pragma kernel Advect
[numthreads(8, 8, 8)]
void Advect(uint3 id : SV_DispatchThreadID)//single value advection
{
	int index = GetIndex(id, size);

	if (obstacles[index] > 0.1f)
	{
		write_R[index] = 0;//if an obstacle is at this grid coord, set velocity to zero
		return;
	}

	float3 advect_pos = GetAdvectedPosition(id, index);
	write_R[index] = max(0, SampleBilinear(read_R, advect_pos, size.xyz) * dissipation - decay);
	//write_R[index] = max(0, read_R[GetIndex(advect_pos, size)] * dissipation - decay);//no bilinear interpolation
}


#pragma kernel AdvectVelocity
[numthreads(8, 8, 8)]
void AdvectVelocity(uint3 id : SV_DispatchThreadID)//for vector advection
{
	int index = GetIndex(id, size);

	if (obstacles[index] > 0.1f)
	{
		write_RGB[index] = float3(0, 0, 0);//if an obstacle is at this grid coord, set velocity to zero
		return;
	}

	float3 advect_pos = GetAdvectedPosition(id, index);
	write_RGB[index] = SampleBilinear(read_RGB, advect_pos, size.xyz) * dissipation;
	//write_RGB[index] = read_RGB[GetIndex(advect_pos, size)] * dissipation;
}